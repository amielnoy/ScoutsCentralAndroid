plugins {
  alias(libs.plugins.android.application)
  alias(libs.plugins.kotlin.android)
}

def localProps = new Properties()
def localPropsFile = rootProject.file("local.properties")
if (localPropsFile.exists()) {
  localProps.load(new FileInputStream(localPropsFile))
}

android {
  namespace 'com.scoutscentral.app'
  compileSdk 36

  defaultConfig {
    applicationId "com.scoutscentral.app"
    minSdk 24
    targetSdk 34
    versionCode 1
    versionName "1.0"

    // Helper to get and clean properties/env vars
    def getSafeProperty = { name ->
        def val = localProps.getProperty(name) ?: System.getenv(name) ?: ""
        // Strip quotes if they were added accidentally to the secret/property
        val = val.replace("\"", "")
        // Escape backslashes for Java string generation
        return val.replace("\\", "\\\\")
    }

    def supabaseUrl = getSafeProperty("SUPABASE_URL")
    def supabaseAnonKey = getSafeProperty("SUPABASE_ANON_KEY")
    
    buildConfigField "String", "SUPABASE_URL", "\"${supabaseUrl}\""
    buildConfigField "String", "SUPABASE_ANON_KEY", "\"${supabaseAnonKey}\""

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  buildFeatures {
    viewBinding true
    buildConfig true
  }

  compileOptions {
    coreLibraryDesugaringEnabled true
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }
  kotlinOptions {
    jvmTarget = '17'
  }
  buildToolsVersion '36.1.0'
}

dependencies {
  coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'

  implementation 'androidx.appcompat:appcompat:1.7.1'
  implementation 'com.google.android.material:material:1.13.0'
  implementation 'androidx.coordinatorlayout:coordinatorlayout:1.3.0'
  implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
  implementation 'androidx.recyclerview:recyclerview:1.4.0'
  implementation 'androidx.lifecycle:lifecycle-viewmodel:2.10.0'
  implementation 'androidx.lifecycle:lifecycle-livedata:2.10.0'
  implementation 'androidx.fragment:fragment:1.8.9'
  implementation 'androidx.viewpager2:viewpager2:1.1.0'
  implementation("androidx.gridlayout:gridlayout:1.1.0")
  implementation 'com.squareup.okhttp3:okhttp:5.3.2'
  implementation 'com.google.code.gson:gson:2.13.2'
  implementation 'com.github.bumptech.glide:glide:5.0.5'
  annotationProcessor 'com.github.bumptech.glide:compiler:5.0.5'

  testImplementation libs.junit
  testImplementation libs.mockito.core
  testImplementation libs.androidx.arch.core.testing

  androidTestImplementation 'androidx.test.ext:junit:1.2.1'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
  androidTestImplementation libs.androidx.arch.core.testing
}
